pub const WRAPPER_CMD_TEMPLATE: &str = r#"@echo off
setlocal enableextensions

rem Generated by `engram init` (Engram version __ENGRAM_VERSION__)
set ENGRAM_VERSION=__ENGRAM_VERSION__
if "%ENGRAM_REPO%"=="" set ENGRAM_REPO=lkubicek1/engram

set ROOT_DIR=%~dp0
cd /d "%ROOT_DIR%"

set ENGRAM_DIR=.engram
set BIN_DIR=%ENGRAM_DIR%\bin
set BIN_PATH=%BIN_DIR%\engram-v%ENGRAM_VERSION%.exe

if not exist "%BIN_DIR%" mkdir "%BIN_DIR%"

rem Only x86_64 is supported by the initial release.
if /i "%PROCESSOR_ARCHITECTURE%"=="AMD64" (
  set ARCH=x86_64
) else (
  echo Unsupported architecture: %PROCESSOR_ARCHITECTURE% 1>&2
  exit /b 1
)

set ASSET=engram-windows-x86_64.exe
set TAG=v%ENGRAM_VERSION%
set CHECKSUM_URL=https://github.com/%ENGRAM_REPO%/releases/download/%TAG%/checksums.txt
set BIN_URL=https://github.com/%ENGRAM_REPO%/releases/download/%TAG%/%ASSET%

if exist "%BIN_PATH%" goto run

set TMP_DIR=%TEMP%\engram-%RANDOM%%RANDOM%
mkdir "%TMP_DIR%" >nul 2>nul

set CHECKSUM_FILE=%TMP_DIR%\checksums.txt
set TMP_BIN=%TMP_DIR%\%ASSET%

where curl >nul 2>nul
if errorlevel 1 (
  echo Error: curl is required to download Engram on Windows. 1>&2
  exit /b 1
)

curl -fsSL "%CHECKSUM_URL%" -o "%CHECKSUM_FILE%"
if errorlevel 1 (
  echo Error: Failed to download checksums. 1>&2
  exit /b 1
)

curl -fsSL "%BIN_URL%" -o "%TMP_BIN%"
if errorlevel 1 (
  echo Error: Failed to download Engram binary. 1>&2
  exit /b 1
)

where powershell >nul 2>nul
if errorlevel 1 (
  echo Error: PowerShell is required for checksum verification. 1>&2
  exit /b 1
)

set EXPECTED=
for /f "usebackq tokens=1" %%H in (`findstr /r /c:"  %ASSET%$" "%CHECKSUM_FILE%"`) do set EXPECTED=%%H
if "%EXPECTED%"=="" (
  echo Error: checksum entry for %ASSET% not found. 1>&2
  exit /b 1
)

set ACTUAL=
for /f "usebackq tokens=1" %%H in (`powershell -NoProfile -Command "(Get-FileHash -Algorithm SHA256 '%TMP_BIN%').Hash.ToLower()"`) do set ACTUAL=%%H
if "%ACTUAL%"=="" (
  echo Error: Failed to compute SHA256 for downloaded binary. 1>&2
  exit /b 1
)

if /i not "%EXPECTED%"=="%ACTUAL%" (
  echo Error: checksum mismatch for %ASSET%. 1>&2
  echo Expected: %EXPECTED% 1>&2
  echo Actual:   %ACTUAL% 1>&2
  exit /b 1
)

copy /y "%TMP_BIN%" "%BIN_PATH%" >nul
if errorlevel 1 (
  echo Error: Failed to write %BIN_PATH%. 1>&2
  exit /b 1
)

:run
"%BIN_PATH%" %*
"#;
